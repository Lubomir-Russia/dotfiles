" Vim configuration by Lubomir-Russia
" Settings are gathered from different books, sites, and people.
"
" To me vi is zen. To use vi is to practice zen. Every command is a koan.
" Profound to the user, unintelligible to the uninitiated. You discover truth
" every time you use it.
"                               - Satish Reddy
"                               From O'Railly - Learning VIM
"
"
" A 'Language' of Text Editing
"
" One way to think about Vim's normal-mode commands is like a language. You
" have 'verbs' and 'nouns'. For example: the 'change' command (c) would be a
" verb and the 'word' item (w) is a noun. You can combine them to form
" imperative sentences that talk about what you want to do with your text.
"
" The wonderful part about this is that whenever you learn a new verb (like
" 'delete' (d)) you can immediately apply it to all the nouns you know, and
" vice versa.
"
" Vim also has 'adjectives' like 'inside' and 'around' (i and a) that let you
" craft sentences like 'change inside parenthesis' (ci( or cib). Once you
" learn one of these you can immediately apply it to all the verbs and nouns
" you already know.
"                               - Steve Losh
"                               From Coming Home to Vim
"                               http://stevelosh.com/blog/2010/09/coming-home-to-vim/

" ~~ Content ~~
  "       = Installation =
  "       = Compatibility =
  "       = Plugin management with tpop's pathogen =
  "       = Extra key binding - mappings =
  "       = Tab mappings =
  "       = Command line abbreviations =
  "       = Undo in insert mode =
  "       = Programming settings =
  "       = General Settings =
  "       = Russian keymaps =
  "       = Plugin specific settings =
  "       = Misc functions and autocmd =

" = Installation =
" ----------------
" Copy or symlink to ~/.vimrc or ~/_vimrc. and make dir for swap files
" ln -nfs ~/bin/dotfiles/vim/vimrc ~/.vimrc
" ln -nfs ~/bin/dotfiles/vim ~/.vim
" mkdir -p ~/.bak/swap
" mkdir -p ~/.bak/vim
" In Windows set variable HOME=%USERPROFILE%
" Copy ctag.exe to C:\ctags\ctags.exe and add this dir to PATH
"
" System wide settings are here: /usr/local/share/vim/vim73


" = Compatibility =
" -----------------
set nocompatible                      " Must come first because it changes other options.

if ( has("win32") )
    " For the sake of gvim menu fonts this section must be before plugins
    " source $VIMRUNTIME/mswin.vim    " Makes gVIM behave more like a Windows
    " behave mswin                    " Makes gVIM behave more like a Windows
    set langmenu=none                 " Set gvim menue in English (must be in the beginning of _vmrc)
    set encoding=utf-8
    let $LANG='en'                    " Set messages in English
    set guioptions-=T                 " Hide toolbar.
    set background=light              " Background.
    set lines=39 columns=110          " Window dimensions.
    " set guioptions-=r                 " Don't show right scrollbar
    let g:showfuncctagsbin="C:\\ctags\\ctags.exe"  " Only in Windows
endif

" This vimrc file is incompatible with vim 7.3 that is not built with
" patch retracile.net/wiki/VimBreakIndent.
" if you see 'E518: Unknown option: breakindent' when start vim 7.3, then
" comment this option :set breakindent


" = Plugin management with tpop's pathogen =
" ------------------------------------------
" Install new plugin: put it's dir into ~/.vim/bundle/ dir
" Remove an plugin:   rm -rf ~/.vim/bundle/plugin-name
" Genrate help for all plugins:  :Helptags
call pathogen#infect()        " Let pathogen to load all plugins
syntax enable                 " Turn on syntax highlighting.
filetype plugin indent on     " Turn on file type detection.
runtime macros/matchit.vim        " Load the matchit plugin.

" = Extra key binding - mappings =
" --------------------------------
" Professor VIM says '87% of users prefer jj over esc', jj abrams disagrees
" imap jj <Esc>  - this mapping jumps to the next line if called at eol.
" This does not work in paste mode
imap jj <Left><Right><Esc>

function! MapForceNormal (key, action)
    execute 'nmap ' . a:key . ' '      . a:action
    execute 'imap ' . a:key . ' <Esc>' . a:action
    execute 'vmap ' . a:key . ' <Esc>' . a:action
    execute 'omap ' . a:key . ' <Esc>' . a:action
endfunction

function! MapRestoreMode (key, action)
    execute 'nmap ' . a:key . ' '      . a:action
    execute 'imap ' . a:key . ' <Esc>' . a:action . 'gi'
    execute 'vmap ' . a:key . ' <Esc>' . a:action . 'gv'
endfunction

function! MapToggleOption (key, option)
    execute 'nmap '.a:key.' :set '      . a:option . '!<CR>:set '.a:option.'?<CR>'
    execute 'imap '.a:key.' <Esc>:set ' . a:option . '!<CR>:set '.a:option.'?<CR>gi'
    execute 'vmap '.a:key.' <Esc>:set ' . a:option . '!<CR>:set '.a:option.'?<CR>gv'
    execute 'omap '.a:key.' <Esc>:set ' . a:option . '!<CR>:set '.a:option.'?<CR>'
endfunction

" On some terminals there is a problem with sending shift-F keys, let's help them
set <S-F6>=[29~
set <S-F7>=[31~
set <S-F8>=[32~
set <S-F9>=[33~
set <S-F10>=[34~

call MapToggleOption("<F5>",  "list")
call MapToggleOption("<F6>",  "number")
call MapToggleOption("<S-F6>","relativenumber")
call MapToggleOption("<F7>",  "spell")            " use ]s, [s, z=, zg, zb
call MapToggleOption("<S-F7>","paste")
call MapToggleOption("<F8>",  "hlsearch")
call MapToggleOption("<S-F8>","wrap")
call MapForceNormal ("<F9>",  ":w<cr>:Shell<space>%:p<cr><cr>") " Save, run script and show output in the scrap window
call MapForceNormal ("<F10>",  ":w<cr>:make<cr>") " Save and run make
" Could not make ShowFunc work in win gVIM
nmap <F11> <Plug>ShowFunc
:imap <S-F7> <nop>
:set pastetoggle=<S-F7>                           " :h pastetoggle for details

" Use Ctrl-F for omni completion
imap <C-F> <C-X><C-O>

" Search & replace the word under the cursor
nmap <C-H> :%s/\<<c-r>=expand("<cword>")<cr>\>/

" In normal mode adds a new line
" If <CR> is used for this then this mapping is incompatible with ShowFunc
" plugin, that uses <CR> for jumps
" nnoremap <CR> i<CR><Esc>
nnoremap S i<CR><Esc>

" < & > to indent blocks
vmap < <gv
vmap > >gv

" \1 to copy a line, comment it with tcomment, go back
call MapRestoreMode("<leader>1","mcyyPgCc`c")

" Ctrl-J to copy a line and go back - like in EditPlus
call MapRestoreMode("<C-J>","mcyyP`c")

" Vim - hjkl-ing while in insert mode, or use TouchCoursor in Windows
inoremap <M-j> <C-O>gj
inoremap <M-k> <C-O>gk
noremap! <M-h> <Left>
noremap! <M-l> <Right>

" Controversial...swap colon and semicolon for easier commands
"nnoremap ; :
"nnoremap : ;
"vnoremap ; :
"vnoremap : ;

" = Tab mappings =
" ----------------
" also use comand abbreviation :e -> tabedit and :n ->tabnew
function! MoveTabLeft()
    let current_tab = tabpagenr()
    if current_tab > 1
        let current_tab = current_tab - 2
        execute 'tabmove' current_tab
    endif
endfunction

function! MoveTabRight()
    let current_tab = tabpagenr()
    execute 'tabmove' current_tab
endfunction

call MapForceNormal("<leader>tt",":tabnew<cr>")
call MapForceNormal("<leader>te",":tabedit<space>")
call MapForceNormal("<leader>tc",":tabclose<cr>")
call MapForceNormal("<leader>to",":tabonly<cr>")
call MapForceNormal("<leader>tf",":tabfirst<cr>")
call MapForceNormal("<leader>ty",":tablast<cr>")
call MapForceNormal("<leader>tm",":tabmove<space>")
call MapForceNormal("<leader>tl",":call MoveTabLeft()<cr>")
call MapForceNormal("<leader>tr",":call MoveTabRight()<cr>")
call MapForceNormal("<leader>tn",":tabnext<cr>")      " useless, use default gt
call MapForceNormal("<leader>tp",":tabprevious<cr>")  " useless, use default gT

" toggle between the current tab and the last active tab
let g:lasttab = 1
nmap <Leader>= :exe "tabn ".g:lasttab<CR>
au TabLeave * let g:lasttab = tabpagenr()

" = Command line abbreviations =
" -----------------------------
cab l ls<cr>:b<space>
cab e tabedit<space>
cab n tabnew<space>

" = Undo in insert mode =
" -----------------------
" Create an undo point after each WORD
" inoremap looks more logical but it brakes the abbreviation, so imap is used
imap <Space> <Space><c-g>u
imap <Tab> <Tab><c-g>u
imap <CR> <CR><c-g>u
" Recover from accidental Ctrl-U and Ctrl-W in insert mode
inoremap <c-u> <c-g>u<c-u>
inoremap <c-w> <c-g>u<c-w>

" = Programming settings =
" ------------------------
" Use perl compiler for all *.pl and *.pm files.
autocmd BufNewFile,BufRead *.p? compiler perl
let g:perl_compiler_force_warnings=0     " = perl -cw % -> prints warnings about dubious constructs
" let g:perl_compiler_force_warnings=1   " = perl -cW % -> all warnings regardless of no warnings

" Set working dir to the dir of the current file, it works better with plugins then set autochdir
autocmd BufEnter * silent! lcd %:p:h

" Read the ctags of the specific project
set tags+=$MATOS/tags,$MyTests/tags;

" To get basic Folding for Perl and PHP working
let perl_fold=1
let perl_fold_blocks = 1
let php_folding=1

" Automatic fold settings for specific files.
autocmd FileType ruby setlocal foldmethod=syntax
autocmd FileType css  setlocal foldmethod=indent shiftwidth=2 tabstop=2

" For the MakeGreen plugin and Ruby RSpec.
autocmd BufNewFile,BufRead *_spec.rb compiler rspec

" = General Settings =
" --------------------
set showcmd                       " Display incomplete commands.
set showmode                      " Display the mode you're in. Default when not vi compatible.
set title                         " Set the terminal's title
" set visualbell                  " No beeping but terminal flash.
set vb t_vb=
" No beeping and no flash.

set ruler                         " Show cursor position.
set showtabline=2                 " Display tabs all of the time
set laststatus=2                  " Show the status line all the time
" Useful status information at the bottom of screen
set statusline=[%n]\ %<%.99f\ %h%w%m%r%y\ %{fugitive#statusline()}%{exists('*CapsLockStatusline')?CapsLockStatusline():''}%=%-16(\ %l,%c-%v\ %)%P

" set number                      " Show (absolute) line numbers.
set relativenumber                " Show relative line number. Useful for motion/deletion commands.
set lazyredraw                    " Do not redaw screen during macros -> mach faster
set confirm                       " Use confirmation dialog instead of error message.

set backspace=indent,eol,start    " Intuitive backspacing.

set hidden                        " Handle multiple buffers better.
" set autoread                    " Reread file automatically if it changes.

set wildmenu                      " Enhanced command line completion.
" set wildmode=list:longest       " Complete files like a shell.
set wildmode=list,full            "	List all matches without completing, then each full match.

set ignorecase                    " Case-insensitive searching.
set smartcase                     " But case-sensitive if expression contains a capital letter.
set incsearch                     " Highlight matches as you type.
set hlsearch                      " Highlight matches.
imap <c-c> <esc>
" Clear the search buffer when hitting return
function! MapCR()
    nnoremap <cr> :nohlsearch<cr>
endfunction
call MapCR()

set nowrap                        " Turn off line wrapping.
set linebreak                     " Wrap at the end of words.
set showbreak=->                  " String to put at the start of lines that have been wrapped.
" set breakindent                 " Works only with patch retracile.net/wiki/VimBreakIndent.
set scrolloff=3                   " Show 3 lines of context around the cursor.
" set colorcolumn=85              " Show a colored column at 85 characters (so I can see when I write a too-long line of code).
" set formatoptions=qrn1          " Different styles of text formating see fo-table for details or just use defaults.

" set browsedir=current           " Dir for file browser. Only for Win GUI and some Unix managers.
" set nobackup                    " Don't make a backup before overwriting a file.
" set nowritebackup               " And again.
" In Windows set local var HOME=%USERPROFILE%
set backup                        " Keep a backup file
set backupdir=$HOME/.bak/vim/     " Keep backup files in one location (it must exist).
set directory=$HOME/.bak/swap//,. " Keep swap files in one location, if not possible then in working dir.
" If a directory ends in two path separators, the swap file name will be built from the complete path to the file with all path separators. This will ensure file name uniqueness in the preserve directory.
" Add the following line to your shell initialization file ~/.bashrc or  ~/.profile :
" find $HOME/.bak/vim/ -name "*" -type f -mtime +60 -exec rm -f {} \;
" When you are logging in all files in the backup directory older then 60 days will be removed. Be shure to backup in shorter terms!

set undofile                      " Create <Filename>.un~ file. These files contain undo information so you can undo previous actions even after you close and reopen a file.
set undodir=$HOME/.bak/undofiles/
" au FocusLost * :wa              " Save all on losing focus.

set tabstop=4                     " Global tab width.
set shiftwidth=4                  " And again, related.
set expandtab                     " Use spaces instead of tabs
set softtabstop=4                 " It feels like <Tab>s are being inserted, while in fact a mix of spaces and <Tab>s is used.
set autoindent                    "  Copy indent from current line when starting a new line.
" Keep tabs instead of spaces in the files: crontab, fstab and make.
autocmd FileType crontab,fstab,make set noexpandtab tabstop=8 shiftwidth=8
" Set list listchars=tab: ,precedes:<,extends:>
set list listchars=precedes:<,extends:>,tab:>-,trail:-
set nolist                        " Do not show list till asked to do it

" set showmatch                   " built-in function replaced with matchIt plugin.

set t_Co=256                      " Use more colors in terminal. May effect some colorshemas.
" Solarized colorschema http://ethanschoonover.com/solarized
" let g:solarized_termcolors=256
set background=dark
colorscheme solarized             " Works best with t_Co=256
" colorscheme topfunky-light
" colorscheme vividchalk
" colorscheme zenburn             " Works best with t_Co=256
" colorscheme murphy              " Works best with t_Co=256 not set

set nofoldenable     " Do not fold when a file is just opened
" set foldlevel=1    " Only first level is opened if foldenable is set.
" set foldcolumn=3   " Show control line.
" set foldopen=all   " Open folds when entering them. Otherwise use <l> to move in.

set modeline         " Allow setting extra option directly in files (default); example: vim: syntax=vim

" = Russian keymaps =
" -------------------

" Vim и русская раскладка клавиатуры
"
" В обычном режиме Vim по умолчанию ожидает, что консоль находится в режиме
" ввода латинских символов. Если вы, к примеру, редактируете с помощью Vim
" текст на русском языке, или в смешанных кодировках (к примеру, HTML-страницу
" на русском), то постоянная необходимость переключать системную клавиатурную
" раскладку очень быстро вам надоест. Существует несколько способов решить эту
" проблему.
" Способ 1 : Сделать так, чтобы русские буквы можно было вводить когда системная
" раскладка находится в режиме ввода латинских символов.
"
set keymap=russian-jcukenwin         " установить keymap, чтобы по Ctrl+^ переключался на русский и обратно
set iminsert=0                       " по умолчанию - латинская раскладка
set imsearch=0                       " по умолчанию - латинская раскладка при поиске
set guifont=courier_new:h17:cRUSSIAN " установить шрифт Courier New Cyr
set iskeyword=@,48-57,_,192-255      " настраиваю для работы с русскими словами (чтобы w, b, * понимали русские слова)
"
" После этого системную раскладку клавиатуры можно будет оставить в режиме
" ввода латинских символов, а переключение между языками осуществлять уже
" внутри самого редактора с помощью команды Ctrl+^ (Ctrl+6 в версии для
" Windows)
"
" Способ 2 : Сделать так, чтобы Vim понимал, какие клавиши нажимаются, если включена русская раскладка.
" set langmap=ёйцукенгшщзхъфывапролджэячсмитьбюЁЙЦУКЕHГШЩЗХЪФЫВАПРОЛДЖЭЯЧСМИТЬБЮ;`qwertyuiop[]asdfghjkl\\;'zxcvbnm\\,.~QWERTYUIOP{}ASDFGHJKL:\\"ZXCVBNM<>
" при этом способе надо помнить и использовать русские знаки припенания.
" Попытка переназначить их приведёт к переназначению и в английской расскладке
" так же. Например переназначив 'русскую' . на / получим что теперь всегда и
" 'английская' . так же будет указывать на /
"
" Если у вас локаль UTF, то данный способ может не работать в старых версиях Vim.
" Необходимо каждый символ назначить индивидуально: http://ru.wikibooks.org/wiki/Vim

" = Plugin specific settings =
" ------------------------------

" Vim-Wiki
let wiki = {}
let g:vimwiki_list = [{'path': '~/bin/dotfiles/wiki/', 'auto_export': 1}] " works both in Unix and Windows
let wiki.nested_syntaxes = {'perl': 'perl', 'bash': 'sh', 'python': 'python', 'c++': 'cpp'} " highlight code syntax

" snipMate
" Update snippets for all buffers when snipfile is saved
autocmd! bufwritepost *snippet* call ReloadAllSnippets()

" = Misc functions and autocmd =
" --------
" Make a section header
nmap <leader>hh gCc0wi=<space><esc>A<space>=<esc>yypwv$:s/./-/g<cr>:nohls<cr>gCcwxx

" Reread .vimrc configuration file after it was saved.
" autocmd! bufwritepost $MYVIMRC source $MYVIMRC
" Same but in case the ~/.vimrc is just a link to a vimrc file in dorfiles.
autocmd! bufwritepost vimrc source $MYVIMRC
autocmd! bufwritepost _vimrc source $MYVIMRC

" if has("autocmd")
"         set viewoptions=cursor,folds
"         autocmd BufWinLeave *.* silent mkview       " Save view of the window.
"         autocmd BufWinEnter *.* silent loadview     " Restore it when open.
" endif

" Jump to the last position of opened file.
autocmd! bufreadpost * call LastPosition()
function! LastPosition()
    if line("'\"") && line("'\"")<=line('$')
        normal! `"
    endif
endfunction

" command :Shell will execute a shell command and put uptput to scrap window
command! -complete=shellcmd -nargs=+ Shell call s:RunShellCommand(<q-args>)
function! s:RunShellCommand(cmdline)
  echo a:cmdline
  let expanded_cmdline = a:cmdline
  for part in split(a:cmdline, ' ')
     if part[0] =~ '\v[%#<]'
        let expanded_part = fnameescape(expand(part))
        let expanded_cmdline = substitute(expanded_cmdline, part, expanded_part, '')
     endif
  endfor
  botright new
  setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile nowrap
  call setline(1, 'You entered:    ' . a:cmdline)
  call setline(2, 'Expanded Form:  ' .expanded_cmdline)
  call setline(3,substitute(getline(2),'.','=','g'))
  execute '$read !'. expanded_cmdline
  " setlocal nomodifiable  " set it if you whant to have truely scrap window
  1
endfunction

" This version of :Shell will execute a shell command and put uptput to window
" with the command name (not practical for perl scripts)
" function! s:ExecuteInShell(command)
"   let command = join(map(split(a:command), 'expand(v:val)'))
"   let winnr = bufwinnr('^' . command . '$')
"   silent! execute  winnr < 0 ? 'botright new ' . fnameescape(command) : winnr . 'wincmd w'
"   setlocal buftype=nowrite bufhidden=wipe nobuflisted noswapfile nowrap number
"   echo 'Execute ' . command . '...'
"   silent! execute 'silent %!'. command
"   silent! execute 'resize ' . line('$')
"   silent! redraw
"   silent! execute 'au BufUnload <buffer> execute bufwinnr(' . bufnr('#') . ') . ''wincmd w'''
"   silent! execute 'nnoremap <silent> <buffer> <LocalLeader>r :call <SID>ExecuteInShell(''' . command . ''')<CR>'
"   echo 'Shell command ' . command . ' executed.'
" endfunction
" command! -complete=shellcmd -nargs=+ Shell call s:ExecuteInShell(<q-args>)

" Convenient command to see the difference between the current buffer and the
" file it was loaded from, thus the changes you made.
" Only define it when not defined already.
if !exists(":DiffOrig")
  command DiffOrig vert new | set bt=nofile | r # | 0d_ | diffthis | wincmd p | diffthis
endif

" Automatically start NERDTree if no file is open.
autocmd VimEnter * if !argc() | NERDTree | endif

" Highlight all instances of word under cursor, when idle.
" Useful when studying strange source code.
" Type z/ to toggle highlighting on/off.
nnoremap z/ :if AutoHighlightToggle()<Bar>set hls<Bar>endif<CR>
function! AutoHighlightToggle()
  let @/ = ''
  if exists('#auto_highlight')
    au! auto_highlight
    augroup! auto_highlight
    setl updatetime=4000
    echo 'Highlight current word: off'
    return 0
  else
    augroup auto_highlight
      au!
      au CursorHold * let @/ = '\V\<'.escape(expand('<cword>'), '\').'\>'
    augroup end
    setl updatetime=500
    echo 'Highlight current word: ON'
    return 1
  endif
endfunction

" LET IT BE AT THE END OF vimrc
" Local extra vimrc setting if any, let it be loaded last:
if filereadable($HOME . "/.vimrc.extra")
    source $HOME/.vimrc.extra
endif
